# Maintainer: ml <>
# Contributor: Martino Pilia <martino.pilia@gmail.com>
pkgname=checkmake-git
pkgver=0.1.0.r39.g1383019
pkgrel=1
pkgdesc='Experimental linter/analyzer for Makefiles'
arch=('x86_64')
url='https://github.com/mrtazz/checkmake'
license=('MIT')
depends=('glibc')
# heavy pandoc, uff
makedepends=('go' 'pandoc' 'git')
conflicts=('checkmake')
source=("$pkgname::git+$url")
sha256sums=('SKIP')

pkgver() { git -C "$pkgname" describe --long --tags | sed 's/-/.r/;s/-g/.g/'; }

prepare() {
  cd "$pkgname"
  go mod vendor
}

build() {
  cd "$pkgname"
  export CGO_ENABLED=1
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  local -a _x
  _x=(
    version="$(git describe --tags --always --dirty)"
    goversion="$(</usr/lib/go/VERSION)"
    buildTime="$(TZ=UTC printf '%(%FT%T)TZ' "$SOURCE_DATE_EPOCH")"
    builder="makepkg"
  )
  go build -ldflags "${_x[*]/#/-X=main.}"
  # man page
  sed "s/REPLACE_DATE/$(TZ=UTC printf '%(%B %d, %Y)T\n' "$SOURCE_DATE_EPOCH")/" man/man1/checkmake.1.md |
    pandoc -s -t man -o checkmake.1
}

check() {
  cd "$pkgname"
  go test -short ./...
}

package() {
  cd "$pkgname"
  install -Dm755 checkmake -t "$pkgdir/usr/bin"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 checkmake.1 -t "$pkgdir/usr/share/man/man1"
  install -Dm644 docs/* -t "$pkgdir/usr/share/doc/$pkgname"
}
