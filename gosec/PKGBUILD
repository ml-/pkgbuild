# Maintainer: ml <>
pkgname=gosec
pkgver=2.8.1
pkgrel=1
pkgdesc='Golang security checker'
arch=('x86_64')
url='https://github.com/securego/gosec'
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('54820f7120265745710f54246ea5cde0fbdd6a9024cec8147f34b3c1855bdcec')

build() {
  local -a x=(
    main.BuildDate="$(TZ=UTC printf '%(%FT%T)TZ' "${SOURCE_DATE_EPOCH:--1}")"
    main.GitTag="v$pkgver"
    main.Version="$pkgver"
  )

  cd "$pkgname-$pkgver"
  export CGO_ENABLED=1
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw'
  go build -ldflags "-linkmode=external ${x[*]/#/-X=}" ./cmd/gosec
}

check() {
  cd "$pkgname-$pkgver"
  go test -short ./...
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 "$pkgname" -t "$pkgdir/usr/bin"
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
