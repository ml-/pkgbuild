# Maintainer: ml <ml@visu.li>
pkgname=go-jira
pkgver=1.0.27
pkgrel=1
pkgdesc='simple jira command line client in Go'
arch=('x86_64' 'i686')
url='https://github.com/go-jira/jira'
license=('Apache')
depends=('glibc')
makedepends=('go')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c5bcf7b61300b67a8f4e42ab60e462204130c352050e8551b1c23ab2ecafefc7')
_bin=${pkgname#go-}

prepare() {
  cd "${_bin}-${pkgver}"
  go mod download
}

build() {
  cd "${_bin}-${pkgver}"
  export CGO_ENABLED=1
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  go build -ldflags="-linkmode=external -X github.com/go-jira/jira.VERSION=$pkgver" ./cmd/jira
}

package() {
  cd "${_bin}-${pkgver}"
  install -Dm755 "${_bin}" -t "${pkgdir}/usr/bin"
  # completion script generation returns 1 for a some reason
  ./${_bin} --completion-script-bash | install -Dm644 /dev/stdin -T "${pkgdir}/usr/share/bash-completion/completions/jira"
  ./${_bin} --completion-script-zsh | install -Dm644 /dev/stdin -T "${pkgdir}/usr/share/zsh/site-functions/_jira"

  # useful README.md
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
