# Maintainer: ml <ml@visu.li>
pkgname=go-jira
pkgver=1.0.23
pkgrel=1
pkgdesc='simple jira command line client in Go'
arch=('x86_64' 'i686')
url='https://github.com/go-jira/jira'
license=('Apache')
# grep -rF exec.Command
depends=('glibc')
makedepends=('go')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('01c86d3119d050774caa41b89fb4f038026542eb1e362e89f3f89bf330b68354')
_bin=${pkgname#go-}

prepare() {
  cd "${_bin}-${pkgver}"
  go mod download
}

build() {
  cd "${_bin}-${pkgver}"
  export CGO_ENABLED=1
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  go build -o jira ./cmd/jira
}

package() {
  cd "${_bin}-${pkgver}"
  install -Dm755 "${_bin}" -t "${pkgdir}/usr/bin"
  # completion script generation returns 1 for a some reason
  ./${_bin} --completion-script-bash | install -Dm644 /dev/stdin -T "${pkgdir}/usr/share/bash-completion/completions/jira" 
  ./${_bin} --completion-script-zsh | install -Dm644 /dev/stdin -T "${pkgdir}/usr/share/zsh/site-functions/_jira"
}
