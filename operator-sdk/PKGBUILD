# Maintainer: ml <ml@visu.li>
pkgname=operator-sdk
pkgver=1.0.1
pkgrel=1
pkgdesc='SDK for building Kubernetes applications'
arch=('x86_64' 'aarch64')
url='https://github.com/operator-framework/operator-sdk'
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
provides=('ansible-operator' 'helm-operator')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('d49d285a5610f207ca4682f30a0e58d1dffe3223e54a91a02bdb1110e0b52cf0')

prepare() {
  cd "${pkgname}-${pkgver}"
  go mod download
}

build() {
  local commit x
  commit=$(bsdcat "${pkgname}-${pkgver}.tar.gz" | git get-tar-commit-id)

  cd "${pkgname}-${pkgver}"
  export CGO_ENABLED=1
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'

  # grab k8s version from Makefile
  [[ $(<Makefile) =~ K8S_VERSION[\ =]+(v[[:digit:].]+) ]] # sets BASH_REMATCH
  x=(
    GitCommit="${commit:?}"
    GitVersion="v${pkgver}"
    KubernetesVersion="${BASH_REMATCH[1]}"
  )
  go build -o . -ldflags "-linkmode=external ${x[*]/#/-X=github.com/operator-framework/operator-sdk/version.}" ./cmd/...
}

check() {
  cd "${pkgname}-${pkgver}"
  chmod 400 "$pkgname"  # canary
  go test -short ./internal/...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 ${pkgname} "${provides[@]}" -t "${pkgdir}/usr/bin"

  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  cp -at "${pkgdir}/usr/share/doc/${pkgname}" example
}
