# Maintainer: ml <ml@visu.li>
pkgname=operator-sdk
pkgver=1.0.0
pkgrel=1
pkgdesc='SDK for building Kubernetes applications'
arch=('x86_64' 'aarch64')
url='https://github.com/operator-framework/operator-sdk'
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
provides=('ansible-operator' 'helm-operator')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('000d92bc519dac41fbe44dd8bbc6d662ff5d7c91b5b38d73b412513ba47987e9')

prepare() {
  cd "${pkgname}-${pkgver}"
  go mod download
}

build() {
  local commit=
  commit=$(bsdcat "${pkgname}-${pkgver}.tar.gz" | git get-tar-commit-id)

  cd "${pkgname}-${pkgver}"
  export CGO_ENABLED=1
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'

  # grab k8s version from Makefile
  [[ $(<Makefile) =~ K8S_VERSION[\ =]+(v[[:digit:].]+) ]] # sets BASH_REMATCH
  local -a x=(
    GitCommit="${commit:?}"
    GitVersion="v${pkgver}"
    KubernetesVersion="${BASH_REMATCH[1]}"
  )
  go build -o . -ldflags "${x[*]/#/-X=github.com/operator-framework/operator-sdk/version.}" ./cmd/...
}

check() {
  cd "${pkgname}-${pkgver}"
  chmod 400 "$pkgname"  # canary
  go test -short ./internal/...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 ${pkgname} "${provides[@]}" -t "${pkgdir}/usr/bin"

  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  cp -at "${pkgdir}/usr/share/doc/${pkgname}" example
}
