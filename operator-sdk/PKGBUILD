# Maintainer: ml <ml@visu.li>
pkgname=operator-sdk
pkgver=0.19.1
pkgrel=1
pkgdesc='SDK for building Kubernetes applications'
arch=('x86_64' 'aarch64')
url='https://github.com/operator-framework/operator-sdk'
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('184c323367dd39d2c6e775702450a9b803dd4b08408812d6d7973dab35973147')

prepare() {
  cd "${pkgname}-${pkgver}"
  go mod download
}

build() {
  local commit=
  commit=$(bsdcat "${pkgname}-${pkgver}.tar.gz" | git get-tar-commit-id)

  cd "${pkgname}-${pkgver}"
  export CGO_ENABLED=1
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'

  # grab k8s version from Makefile
  [[ $(<Makefile) =~ K8S_VERSION[\ =]+(v[[:digit:].]+) ]] # sets BASH_REMATCH
  local -a x=(
    GitCommit="${commit:?}"
    GitVersion="v${pkgver}"
    KubernetesVersion="${BASH_REMATCH[1]}"
  )
  go build -o "${pkgname}" -ldflags "${x[*]/#/-X=github.com/operator-framework/operator-sdk/version.}" ./cmd/"${pkgname}"
}

check() {
  cd "${pkgname}-${pkgver}"
  go test -short ./cmd/operator-sdk/... ./internal/... ./pkg/...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 ${pkgname} -t "${pkgdir}/usr/bin"
}
