# Maintainer: ml <>
# Maintainer: Kaizhao Zhang <zhangkaizhao[AT]gmail.com>

pkgname=redoc-cli
pkgver=0.12.1
pkgrel=1
pkgdesc="ReDoc's Command Line Interface"
arch=('any')
url="https://github.com/Redocly/redoc"
license=('MIT')
depends=('nodejs')
makedepends=('modclean' 'npm')
source=("https://registry.npmjs.org/${pkgname}/-/${pkgname}-${pkgver}.tgz")
sha256sums=('8df736c34b772dd94998f33a3e2ce6eae8863071f49795be16d62400df0e1880')
noextract=("${pkgname}-${pkgver}.tgz")

package() {
  export CI=1
  npm install --no-fund --no-audit -g --prefix "${pkgdir}/usr" "${srcdir}/${pkgname}-${pkgver}.tgz"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "${pkgdir}"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "${pkgdir}"

  modclean --run --path="$pkgdir"/usr/lib/node_modules \
    --additional-patterns="__tests__,man,.github,.husky,Dockerfile,*.un~,VERSION,*.lock,uritemplate-test"
  find "$pkgdir"/usr/lib/node_modules -type d -empty -delete -print
}
