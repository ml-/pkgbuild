# Maintainer: ml <ml@visu.li>
pkgname=helm-plugin
pkgver=1.0.0
pkgrel=1
pkgdesc='Helm plugin to plug in Helm'
arch=('x86_64')
url='https://github.com/example/helm-plugin'
license=('Apache')
install=helm-plugin.install
depends=('helm')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('4bfd50f1e6facc19d988c6594ec77d813eca72c9800815991085561c41e149a4')

prepare() {
  cd "${pkgname}-${pkgver}"
  go mod download
  # remove hooks from plugin file, so they are not executed on helm plugin install
  sed -i '/^hooks:$/Q' plugin.yaml
}

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_ENABLED=1
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  go build -o plugin -ldflags "-linkmode=external -X main.version=${pkgver}" ./cmd/plugin
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
  # (optional) check if define was really set
  grep -Fqm1 "$pkgver" plugin
}

package() {
  cd "${pkgname}-${pkgver}"
  # strip helm- prefix from directory
  install -Dm755 plugin -t "${pkgdir}/usr/lib/helm/plugins/${pkgname##helm-}/bin"
  install -m644 plugin.yaml -t "${pkgdir}/usr/lib/helm/plugins/${pkgname##helm-}"
  #install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
