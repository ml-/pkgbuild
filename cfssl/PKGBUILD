# Maintainer: ml <>
pkgname=cfssl
pkgver=1.6.0
pkgrel=1
pkgdesc="Cloudflare's PKI and TLS toolkit"
arch=('x86_64')
url='https://github.com/cloudflare/cfssl'
license=('BSD')
depends=('glibc')
makedepends=('go')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('9694e84b42bf3fc547fa5ac5b15099da28129b9b274b3aad3534d7145ca1cc04')

build() {
  cd "$pkgname-$pkgver"
  export CGO_ENABLED=1
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw'
  mkdir -p bin
  go build -o bin/ -ldflags "-linkmode=external -X github.com/cloudflare/cfssl/cli/version.version=$pkgver" ./cmd/...
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm755 bin/* -t "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/doc"
  cp -a doc -T "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
