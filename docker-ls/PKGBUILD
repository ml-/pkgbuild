# Maintainer: ml <ml@visu.li>
# Contributor: Lukas Zimmermann <luk.zim91 at gmail dot com>
pkgname=docker-ls
pkgver=0.5.1
pkgrel=1
pkgdesc='Tools for browsing and manipulating docker registries'
url='https://github.com/mayflower/docker-ls'
license=('MIT')
arch=('x86_64' 'i686' 'aarch64' 'arm' 'armv6h' 'armv7h')
depends=('glibc')
makedepends=('go')
provides=('docker-rm')
source=("$url/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('53e6bf3f7b24d37f1038e2ed7dedbf7608d314c93f60f325e6fe7ab3883c5881')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_ENABLED=1
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw'
  go build -o . -ldflags="-linkmode=external -X github.com/mayflower/docker-ls/lib.staticVersion=$pkgver" ./cli/docker-ls  ./cli/docker-rm

  # do not try to load config in HOME
  local HOME=/dev/null
  for i in docker-{ls,rm}; do
    ./"$i" autocomplete bash >"$i".bash
    ./"$i" autocomplete zsh >"$i".zsh
  done
}

check() {
  cd "${pkgname}-${pkgver}"
  go test -short ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  for i in docker-{ls,rm}; do
    install -Dm755 "$i" -t "$pkgdir/usr/bin"
    install -Dm644 "$i".bash "${pkgdir}"/usr/share/bash-completion/completions/"$i"
    install -Dm644 "$i".zsh "$pkgdir"/usr/share/zsh/site-functions/_"$i"
  done
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
