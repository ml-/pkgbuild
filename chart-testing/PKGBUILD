# Maintainer: ml <>
pkgname=chart-testing
pkgver=3.3.1
_pkgtag=${pkgver/_/-}
pkgrel=1
pkgdesc='CLI tool for linting and testing Helm charts'
arch=('x86_64' 'aarch64' 'armv6h')
url='https://github.com/helm/chart-testing'
license=('Apache')
depends=('kubectl' 'git' 'yamllint' 'helm' 'yamale')
makedepends=('go')
source=("${url}/archive/v${_pkgtag}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('7b48e30ac365ccdc0c91c2d93bc9e264c4cd43df779b955063ecd874490f0186')
backup=('etc/ct/lintconf.yaml')

prepare() {
  cd "${pkgname}-${_pkgtag}"
  go get -d -modcacherw ./...
}

build() {
  local commit=
  commit=$(bsdcat "${pkgname}-${pkgver}.tar.gz" | git get-tar-commit-id)
  local -a x=(
    BuildDate="$(TZ=UTC printf '%(%FT%T)TZ' "$SOURCE_DATE_EPOCH")"
    GitCommit="${commit:?}"
    Version="v${pkgver}"
  )
  cd "${pkgname}-${_pkgtag}"
  export CGO_ENABLED=1
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -mod=readonly -modcacherw -trimpath'
  go build -o bin/ct -ldflags "-linkmode=external ${x[*]/#/-X=github.com/helm/chart-testing/v3/ct/cmd.}" ./ct
}

check() {
  cd "${pkgname}-${_pkgtag}"
  go test -ldflags "-linkmode=external ${x[*]/#/-X=github.com/helm/chart-testing/v3/ct/cmd.}" -short ./...
}

package() {
  cd "${pkgname}-${_pkgtag}"
  install -Dm755 bin/ct -t "${pkgdir}/usr/bin"
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 etc/lintconf.yaml etc/chart_schema.yaml -t "${pkgdir}/etc/ct"
  cp -a doc -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a examples -t "${pkgdir}/usr/share/doc/${pkgname}"
}
